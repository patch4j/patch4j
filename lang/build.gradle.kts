/*
 * Copyright 2026 The patch4j Authors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
import com.strumenta.antlrkotlin.gradle.AntlrKotlinTask

plugins {
    alias(libs.plugins.patch4j.kotlin)
    alias(libs.plugins.antlr4.kotlin)
    alias(libs.plugins.patch4j.test.fixtures)
}

dependencies {
    api(projects.model)
    testImplementation(testFixtures(projects.model))
}

val antlrGeneratedPackage = "io.github.patch4j.lang.generated"
val antlrGeneratedDir = "generatedAntlr/${antlrGeneratedPackage.replace(".", "/")}"

val generateKotlinGrammarSource = tasks.register<AntlrKotlinTask>("generateKotlinGrammarSource") {
    description = "Generate kotlin grammar source"
    dependsOn("cleanGenerateKotlinGrammarSource")

    source = fileTree(layout.projectDirectory.dir("antlr")) {
        include("**/*.g4")
    }

    packageName = antlrGeneratedPackage
    arguments = listOf("-visitor")
    outputDirectory = layout.buildDirectory.dir(antlrGeneratedDir).get().asFile
}

kotlin {
    sourceSets {
        main {
            kotlin {
                srcDir(layout.buildDirectory.dir(antlrGeneratedDir))
            }
            dependencies {
                implementation(libs.antlr.kotlin.runtime)
            }
        }
    }
}
